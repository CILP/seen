<html>
<head>
  <title>Mocap-Driven Skeleton | seen.js</title>
  
  <link href="http://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css"/>
  
  <link href="css/theme.css" rel="stylesheet" type="text/css"/>
  
  
  <script src="lib/seen.min.js"></script>
  
  <script src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.7.1/coffee-script.min.js"></script>
  
</head>
<body>
  <div class="page">
    <h1>Mocap-Driven Skeleton</h1>
    <h4></h4>

        <div class="nav-bar">
      <div class="nav-button-wrapper" style="text-align : left;">
        
          <a href="demo-gravity.html"><div class="nav-button">
          <div class="label">prev</div><div class="title">N-Body Gravity Simulation</div>
          </div></a>
        
      </div>
      <div class="nav-button-wrapper" style="text-align : center;">
          <a href="index.html"><div class="nav-button">
          <div class="label">up</div><div class="title">to index</div>
          </div></a>
      </div>
      <div class="nav-button-wrapper" style="text-align : right;">
        
          <a href="demo-2048.html"><div class="nav-button">
          <div class="label">next</div><div class="title">2048</div>
          </div></a>
        
      </div>
    </div>

    
    <canvas width="900" height="500" id="seen-canvas"></canvas>
    

    <div class="caption">
    
<p>Drag to rotate. Mousewheel to zoom.</p>

    </div>

    
<script src="http://cdnjs.cloudflare.com/ajax/libs/lodash.js/2.4.1/lodash.legacy.js"></script>
<script src="http://ajax.googleapis.com/ajax/libs/jquery/1.11.0/jquery.min.js"></script>
<script type="text/coffeescript" id="code">
  width  = 900
  height = 500

  # Create empty scene
  scene = new seen.Scene
    cullBackfaces    : false
    fractionalPoints : true
    model            : seen.Models.default()
    viewport         : seen.Viewports.center(width, height)

  seenModel = scene.model.append()

  # Add mouse-rotate
  dragger = new seen.Drag('seen-canvas', {inertia : true})
  dragger.on('drag.rotate', (e) ->
    xform = seen.Quaternion.xyToTransform(e.offsetRelative...)
    seenModel.transform(xform)
  )

  # Add mousewheel-zoom
  zoomer = new seen.Zoom('seen-canvas', {smooth : false})
  zoomer.on('zoom.scale', (e) ->
    xform = seen.M().scale(e.zoom)
    seenModel.transform(xform)
  )

  # Create a context with a dark fill layer and the scene layer
  context = seen.Context('seen-canvas')
  context.layer(new seen.FillLayer(width, height, '#444'))
  context.sceneLayer(scene)
  context.render()

  # Define a 'Shape Factory' for creating the shapes in our mocap model
  shapeFactory = (joint, endpoint) ->
    radius = switch true
      when /thumb|index|mid|ring|pinky/ig.test(joint.id) then 0.4
      when /hand/ig.test(joint.id) then 1
      when /abdomen|chest/ig.test(joint.id) then 6
      when /hip/ig.test(joint.id) then 4
      else 2

    color = switch true
      when /foot|hip|abdomen|chest/ig.test(joint.id) then '#FF88FF'
      when /shin/ig.test(joint.id) then '#FFFFFF'
      else '#FFD2A6'

    return seen.Shapes.pipe(seen.P(), endpoint, radius, 8).fill(color)

  $.get 'assets/05_11.bvh', {}, (contents) ->
    # Parse the file and generate the shapes and transforms
    mocap = seen.Mocap.parse(contents).createMocapModel(shapeFactory)

    # Add the shapes to our scene
    seenModel.append().translate(0,-50,0).add(mocap.model)

    # Animate the frame from the file
    animator = new seen.MocapAnimator(mocap)
    animator.onFrame(context.render)
    animator.start()

</script>


    
    <link href="http://yandex.st/highlightjs/8.0/styles/monokai_sublime.min.css" rel="stylesheet" type="text/css"/>
<script src="http://yandex.st/highlightjs/8.0/highlight.min.js"></script>
<pre><code id="code-view"></code></pre>
<script type="text/javascript">
  var view = document.getElementById('code-view');
  var code = document.getElementById('code');
  view.innerHTML = code.innerHTML;
  hljs.highlightBlock(view);
</script>
    
  </div>
</body>
</html>
